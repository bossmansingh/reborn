buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.41"
        classpath "org.jacoco:org.jacoco.core:0.8.4"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.18"
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply from: "$rootDir/gradle/ktlint.gradle"
    apply from: "$rootDir/gradle/tests.gradle"

    group = "com.sukhaikoh.reborn"
    version = project.properties["release.version"]

    repositories {
        jcenter()
        mavenCentral()
    }

    sourceCompatibility = "8"
    targetCompatibility = "8"

    jacoco {
        toolVersion = "0.8.4"
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }

        def javaClasses = fileTree(dir: "${buildDir}/classes/java/main")
        def kotlinClasses = fileTree(dir: "${buildDir}/classes/kotlin/main")

        classDirectories = files([javaClasses, kotlinClasses])
        sourceDirectories = files([
                "${project.projectDir}/src/main/java",
                "${project.projectDir}/src/main/kotlin"
        ])

        executionData = files("${project.buildDir}/jacoco/test.exec")

        afterEvaluate {
            classDirectories = files(classDirectories.files.collect {
                fileTree(dir: it)
            })
        }
    }

    jacocoTestReport.dependsOn test

    dependencies {
        implementation fileTree(dir: 'libs', include: ['*.jar'])

        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    }
}